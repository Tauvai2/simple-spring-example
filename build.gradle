group 'info.ernestas.simple'
version '0.1.0'

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.13'
        compile 'org.slf4j:slf4j-simple:1.7.13'

        testCompile 'junit:junit:4.12'
        testCompile 'org.springframework:spring-test:4.2.4.RELEASE'
        testCompile 'org.hamcrest:hamcrest-all:1.3'
    }

    configurations {
        integrationTestsCompile.extendsFrom testCompile
        integrationTestsRuntime.extendsFrom testRuntime
    }

    sourceSets {
        integrationTests {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integration-test/java')
            }
            resources.srcDir file('src/integration-test/resources')
        }
    }

    idea {
        module {
            // Show integration test source directory in IntelliJ IDEA
            testSourceDirs += file('src/integration-test/java')
            scopes.TEST.plus += [ configurations.integrationTestsCompile ]
        }
    }

    tasks.withType(Test) {
        // Print to console all the test which were started, passed, failed or were skipped
        testLogging {
            events 'started', 'passed', 'failed', 'skipped'
        }
    }

    task integrationTests(type: Test) {
        testClassesDir = sourceSets.integrationTests.output.classesDir
        classpath = sourceSets.integrationTests.runtimeClasspath
    }

    check.dependsOn integrationTests
    integrationTests.mustRunAfter test
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}