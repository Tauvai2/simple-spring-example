group 'info.ernestas.simple'
version '0.0.1'

allprojects {
    apply plugin: 'java'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile 'junit:junit:4.12'
        testCompile 'org.springframework:spring-test:4.2.4.RELEASE'
    }

    configurations {
        integrationTestsCompile.extendsFrom testCompile
        integrationTestsRuntime.extendsFrom testRuntime
    }

    sourceSets {
        integrationTests {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            java.srcDir file('src/integration-test/java')
            resources.srcDir file('src/integration-test/resources')
        }
    }
    task integrationTests(type: Test) {
        testClassesDir = sourceSets.integrationTests.output.classesDir
        classpath = sourceSets.integrationTests.runtimeClasspath
    }

    check.dependsOn integrationTests
    integrationTests.mustRunAfter test
}